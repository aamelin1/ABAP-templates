
Simple way to show ALV in OO style:
``` abap
REPORT zfi_samples.  
TABLES: tcurt.  
  
SELECT-OPTIONS: so_sprasÂ  FOR tcurt-spras DEFAULT sy-langu.  
PARAMETERS: Â  Â  p_var Â  Â  TYPE slis_vari.  
  
*create a new local class fo event handler 
CLASS cl_event_handler DEFINITION.  
Â  PUBLIC SECTION.  
Â  Â  CLASS-METHODS on_link_click FOR EVENT link_click OF cl_salv_events_table  
Â  Â  Â  IMPORTING row column.  
ENDCLASS.  
  
INITIALIZATION.  
Â  DATA(ls_key) = VALUE salv_s_layout_key( report = sy-repid ).  
Â  p_var = cl_salv_layout_service=>get_default_layout( s_key = ls_key )-layout. "set def layout at sscr 
  
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_var.  
Â  p_var = cl_salv_layout_service=>f4_layouts( s_key = ls_key restrict = if_salv_c_layout=>restrict_none )-layout.  
  
START-OF-SELECTION.  
Â  "Select data 
Â  SELECT *  
Â  Â  FROM tcurt 
Â  Â  INTO TABLE @DATA(it_rep)  
Â  Â  WHERE spras IN @so_spras.  
Â  SALV 
Â  cl_salv_table=>factory( ##EXCP_UNHANDLED[CX_SALV_MSG]  
Â  Â  IMPORTING  
Â  Â  Â  r_salv_table = DATA(o_alv)  
Â  Â  CHANGING  
Â  Â  Â  t_tableÂ  Â  Â  = it_rep[] ).  
  
Â  o_alv->get_display_settings( )->set_list_header( 'Header text' ).  
Â  o_alv->get_layout( )->set_key( ls_key ).  
Â  o_alv->get_layout( )->set_save_restriction( cl_salv_layout=>restrict_none ).  
Â  o_alv->get_layout( )->set_default( abap_true ). "allow to save def layouts  
Â  o_alv->get_layout( )->set_initial_layout( p_var ). "set layout from sscr  
Â  o_alv->get_functions( )->set_all( abap_true ). "Enable function buttons  
Â  o_alv->get_columns( )->set_optimize( abap_true ). "Optimize Column  
Â  o_alv->get_display_settings( )->set_striped_pattern( cl_salv_display_settings=>true ). "Enable Zebra style  
Â  o_alv->get_selections( )->set_selection_mode( if_salv_c_selection_mode=>multiple ). "Selection mode: SINGLE/MULTIPLE/CELL  
Â  TRY.  
Â  Â  Â  o_alv->get_columns( )->get_column( 'SPRAS' )->set_long_text( 'Long Name' ). "Set column names manualy  
Â  Â  Â  o_alv->get_columns( )->get_column( 'SPRAS' )->set_medium_text( 'Mid Name' ).  
Â  Â  Â  o_alv->get_columns( )->get_column( 'SPRAS' )->set_short_text( 'Sh Name' ).  
Â  Â  Â  CAST cl_salv_column_table( o_alv->get_columns(Â  )->get_column( 'SPRAS' ) )->set_cell_type( if_salv_c_cell_type=>hotspot ). "Set hotspot  
Â  Â  Â  CAST cl_salv_column_table( o_alv->get_columns(Â  )->get_column( 'KTEXT' ) )->set_color( VALUE lvc_s_colo( col = col_total int = 1 inv = 0 ) ). "Set color  
Â  Â  CATCH cx_salv_not_found.  
Â  ENDTRY.  
Â  SET HANDLER cl_event_handler=>on_link_click FOR o_alv->get_event( ). "handler for hotspots 
Â  o_alv->display( ). "Display ALV  
Â  
"Handler implementation 
Â  CLASS cl_event_handler IMPLEMENTATION. "hotspot click  
Â  Â  METHOD on_link_click.  
Â  Â  Â  READ TABLE it_rep INTO DATA(ls_rep) INDEX row.  
Â  Â  Â  CHECK sy-subrc = 0.  
Â  Â  Â  CASE column.  
Â  Â  Â  Â  WHEN 'SPRAS'.  
Â  Â  Â  Â  Â  MESSAGE 'test' TYPE 'I'.  
Â  Â  Â  ENDCASE.  
Â  Â  ENDMETHOD.  
Â  ENDCLASS.
```


 > **ðŸ’¡ Notes**
 > [ALV Options (colors, selctions etc.)](ALV_Other_attr.md)
 
 